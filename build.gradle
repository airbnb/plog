apply plugin: 'wrapper'
apply plugin: 'idea'

idea.module {
    downloadJavadoc = true
    downloadSources = true
}

ext {
    gitVersion = new ByteArrayOutputStream().with { out ->
        exec {
            commandLine 'git', 'describe', '--tags', '--dirty'
            standardOutput = out
        }
        final full = out.toString().trim()
        // 'v1.0' -> '1.0', 'vendetta' -> 'vendetta'
        full ==~ /v\d/ ? full.substring(1) : full
    }
}

subprojects {
    apply plugin: 'project-report'
    apply plugin: 'groovy'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'bintray'

    version = rootProject.ext.gitVersion

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    group = 'com.airbnb.plog'

    ext {
        nettyVersion = '4.0.19.Final'
        slf4jVersion = '1.7.7'
        metricsVersion = '3.0.2'
    }

    dependencies {
        compile 'org.projectlombok:lombok:1.12.6'
        compile 'com.google.guava:guava:17.0'
        compile 'com.typesafe:config:1.2.0'
        compile 'com.eclipsesource.minimal-json:minimal-json:0.9.1'
        compile "io.netty:netty-codec:${nettyVersion}"
        compile "io.netty:netty-handler:${nettyVersion}"
        compile "org.slf4j:slf4j-api:${slf4jVersion}"

        testCompile 'org.codehaus.groovy:groovy-all:2.2.2'
        testCompile 'junit:junit:4.11'
    }

    task cp << {
        println project.sourceSets.test.runtimeClasspath.asPath
    }

    publishing.publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    bintray {
        user = project.hasProperty('bintrayUser') ? project.getProperty('bintrayUser') : null
        key  = project.hasProperty('bintrayKey')  ? project.getProperty('bintrayKey')  : null

        publications = ['mavenJava']

        pkg {
            userOrg = 'airbnb'
            repo = 'jars'
            name = 'plog'
            licenses = ['Apache-2.0']
        }
    }

    repositories {
        mavenLocal()
        jcenter()
    }

}

buildscript {
    repositories { jcenter() }
    dependencies { classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3' }
}
