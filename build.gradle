apply plugin: 'wrapper'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'
apply plugin: 'shadow'
apply plugin: 'project-report'

group = 'com.airbnb.plog'
version = '1.1'
mainClassName = 'com.airbnb.plog.App'
sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories { jcenter() }

dependencies {
    compile 'org.projectlombok:lombok:1.12.4'
    compile 'io.netty:netty-handler:4.0.14.Final'
    compile 'io.netty:netty-codec:4.0.14.Final'
    compile 'org.javassist:javassist:3.18.1-GA'
    compile 'com.google.guava:guava:16.0.1'
    compile 'com.typesafe:config:1.2.0'
    compile 'org.slf4j:slf4j-api:1.7.6'
    compile 'log4j:log4j:1.2.17'
    compile 'org.xerial.snappy:snappy-java:1.1.0.1'
    compile('org.apache.kafka:kafka_2.10:0.8.0') {
        exclude module: 'zookeeper'
        exclude module: 'zkclient'
        exclude module: 'jmxri'
        exclude module: 'jmxtools'
    }
    testCompile 'org.codehaus.groovy:groovy-binary:2.2.1'
}

jar { manifest { attributes 'Main-Class': 'com.airbnb.plog.App' } }

shadow { artifactSet { exclude '*:lombok:*' } }

idea.module {
    downloadJavadoc = true
    downloadSources = true
}

run {
    maxHeapSize = '128M'
    jvmArgs = [
        '-XX:+UseConcMarkSweepGC',
        '-XX:+AggressiveOpts',
        '-XX:+UseCompressedOops',
        '-XX:+UseFastEmptyMethods',
        '-XX:+UseFastAccessorMethods',
        '-XX:CompileThreshold=1500',
    ]

    systemProperties = [
// the kafka binary distrib doesn't seem to include snappy support
//      'compression.codec': 'snappy',
        'producer.type': 'async',
        'queue.buffering.max.ms': 500,
        'request.required.acks': 1,
    ]
}

buildscript {
    repositories { jcenter() }
    dependencies { classpath 'com.github.jengelman.gradle.plugins:shadow:0.8' }
}
