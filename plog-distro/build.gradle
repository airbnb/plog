apply plugin: 'application'
apply plugin: 'shadow'

mainClassName = 'com.airbnb.plog.PlogServer'

dependencies {
    compile project(':plog-kafka')
    compile project(':plog-server')
    compile project(':plog-console')

    // We love logback at Airbnb. Kill log4j.
    compile 'ch.qos.logback:logback-classic:1.1.2'
    compile 'org.slf4j:log4j-over-slf4j:1.7.7'

    // performance improvements for netty
    compile 'org.javassist:javassist:3.18.1-GA'

    compile 'org.xerial.snappy:snappy-java:1.1.0.1'
}

jar {
    manifest {
        attributes 'Main-Class': 'com.airbnb.plog.PlogServer',
                'Plog-Version': version
    }
}

shadow {
    artifactSet {
        exclude '*:lombok:*'
        exclude '*:jsr305:*'
    }
}

run {
    maxHeapSize = '128M'
    jvmArgs = [
            '-XX:+UseConcMarkSweepGC',
            '-XX:+AggressiveOpts',
            '-XX:+UseCompressedOops',
            '-XX:+UseFastEmptyMethods',
            '-XX:+UseFastAccessorMethods',
    ]
}

buildscript {
    repositories { jcenter() }
    dependencies { classpath 'com.github.jengelman.gradle.plugins:shadow:0.8' }
}
